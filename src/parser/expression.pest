TEMPLATE = { (INTERPOLATED | LITERAL)+ }
INTERPOLATED = { INTERPOLATION_START ~ LOGIC ~ INTERPOLATION_END }
GROUP = { GROUP_START ~ LOGIC ~ GROUP_END }

// Operators
UNARY_OP = { "-" | "!" }
LOGIC_OP = { "&&" | "||" }
COMPARISON_OP = { "==" | "!=" | "<=" | ">=" | "<" | ">" }
MATH_PE_OP = { "^" }
MATH_MD_OP = { "*" | "/" | "%" }
MATH_AS_OP = { "+" | "-" }

// Expressions
LOGIC = { COMPARE ~ (LOGIC_OP ~ COMPARE)* }
COMPARE = { MATH ~ (COMPARISON_OP ~ MATH)* }
MATH = { MULTIPLY ~ (MATH_AS_OP ~ MULTIPLY)* }
MULTIPLY = { EXPONENT ~ (MATH_MD_OP ~ EXPONENT)* }
EXPONENT = { TERM ~ (MATH_PE_OP ~ TERM)* }
TERM = { UNARY_OP? ~ (GROUP | NUMBER | REFERENCE) }

// Literals and References
LITERAL = _{ BOOL | NUMBER | STRING }
BOOL = { "true" | "false" }
NUMBER = @{ ASCII_DIGIT+ ~ (DOT ~ ASCII_DIGIT+)? }
STRING = @{ QUOTED_STRING | BARE_STRING }
QUOTED_STRING = _{ QUOTE ~ (ESCAPED_SEQUENCE | TEMPLATE | NON_QUOTE_CHAR)* ~ QUOTE }
BARE_STRING = _{ (!INTERPOLATION_START ~ ANY)+ }
REFERENCE = @{ IDENTIFIER ~ (DOT ~ IDENTIFIER)* }
IDENTIFIER = @{ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT)* }

// Auxiliary Rules
DOT = _{ "." }
QUOTE = _{ "\"" }
INTERPOLATION_START = _{ "${" }
INTERPOLATION_END = _{ "}" }
GROUP_START = _{ "(" }
GROUP_END = _{ ")" }
ESCAPED_SEQUENCE = _{ "\\" ~ (QUOTE | INTERPOLATION_START) }
NON_QUOTE_CHAR = { !QUOTE ~ ANY }
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
